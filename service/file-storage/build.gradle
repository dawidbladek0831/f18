plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'pl.app.file'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations.configureEach {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
}

dependencies {
    // SUBPROJECTS
    implementation project(':common:shared')
    implementation project(':common:mapper')

    // WEB
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // SECURITY
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    // DATABASE
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // KAFKA
    implementation 'org.apache.kafka:kafka-streams'
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // MAPPER
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.modelmapper.extensions:modelmapper-spring:3.1.1'

    // LOMBOK
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // TESTS
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // REACTOR
    implementation platform('io.projectreactor:reactor-bom:2023.0.8')
    testImplementation 'io.projectreactor:reactor-test'

    // MOCKITO
    testImplementation 'org.mockito:mockito-core:5.12.0'

    // TEST CONTAINER
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
    testImplementation "org.testcontainers:testcontainers:1.20.1"
    testImplementation "org.testcontainers:junit-jupiter:1.20.1"
    testImplementation "org.testcontainers:mongodb:1.20.1"

    // BUGFIX
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.0'
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
}
tasks.jar {
    manifest {
        archiveFileName.set("file-storage-service.jar")
    }
}